REM A.REG = $030C
REM X.REG = $030D
REM Y.REG = $030E
REM C.REG = $030F
REM VERA.LOWADDR=$9F20
REM VERA.MIDADDR=$9F21
REM VERA.HIGHADDR=$9F22:
REM VERA.HIADDR=VERA.HIGHADDR
REM MCIOUT = $FEB1
REM CHKOUT = $FFC9
REM CLRCHN = $FFCC


5 DEF FN SW(W)=W + 65536 * (W > 32767)
6 DEF FN UW(W)=W-65536*(W<0)
7 DEF FN LB(X)=(FN SW(X)) AND $FF
8 DEF FN HB(X)=INT(FN UW(X)/256)


REM CALL WITH F$=DESIRED FILENAME
REM        VB --  Video Bank (1 or 0)
REM        VA --  Video Ram address with VB makes complete address
REM        NB --  Number of Bytes to save (Multiples of 128 will be fastest)
REM        F$ --  Filename to save, WILL be overwritten.

50000  VM = FN HB(VA)
50010  VL = FN LB(VA)
50020  FF$="@:"+F$+",S,W"
50030  OPEN 5,8,5,FF$
50040  POKE X.REG,5:SYS $FFC9
50050  POKE $9F20,VL
50060  POKE $9F21,VM
50070  POKE $9F22, %00010000 + VB
50080  L1 = INT(NB/128)
50090  L2 = NB - (128*L1)
50100  IF L1=0 THEN 50180
50110  FOR I = 1 TO L1
50120     POKE $030C, 128
50130     POKE $030E, $9F
50140     POKE $030D,$23
50150     POKE $030F,1
50160     SYS $FEB1
50170  NEXT
50180 IF L2=0 THEN 50260
50190  FOR I = 1 TO L2
50200     POKE $030C, 1
50210     POKE $030E, $9F
50220     POKE $030D,$23
50230     POKE $030F,1
50240     SYS $FEB1
50250  NEXT
50260  SYS $FFCC
50270  CLOSE 5
50280  RETURN
