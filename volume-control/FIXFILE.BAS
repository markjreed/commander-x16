#CONTROLCODES 1
#INCLUDE "BOOKS.DEF"




GOSUB VERIFY.GOOD.LOAD
GOSUB GetCWD$
GOSUB GET.TARGET.DIR
POP.DIR$=CWD$
COMMAND$="CD:"+TargetDir$:GOSUB DOS.CMD
GOSUB CHECK.ERROR

GOSUB CHECK.FOR.FILE:
 F$=THEFILE$:GOSUB FILEEXISTS
 IF NOT FE% THEN WAS.ERROR


## X16 EDIT API, USE RAM BANKS STARING
## AT BANK 10
  POKE X.REG, 10
  POKE Y.REG, (SAVEBANK-5)
  P=POINTER(THEFILE$)
  POKE R0L, PEEK(P+1)
  POKE R0H, PEEK(P+2)
  POKE R1L, PEEK(P)
  BANK 0,13:SYS $C006

EFILE$=THEFILE$+",S,R"
OPEN 4,DEVICE,4,EFILE$
GOSUB CHECK.ERROR
GOSUB SET.LINE.END
IF Terminator=10 OR IS.WINDOWS.FILE THEN GOSUB FIX.THE.FILE
CLOSE 4

COMMAND$ = "CD:" + POP.DIR$
GOSUB DOS.CMD
GOSUB CHECK.ERROR
PAD$=MyRootDir$
IF PAD$<>"/" THEN PAD$=PAD$+"/"
PROG$=PAD$+"BOOKMENU.PRG"
LOAD PROG$
END

FIX.THE.FILE:
  PRINT
  PRINT "FIXING LINE ENDINGS"
  PRINT
  GOSUB Make.Temp.File
  GOSUB CHECK.ERROR
  COLOR VGA.GREEN
  TMP$="@:"+TmpFile$+",S,W"
  OPEN 7,DEVICE,7,TMP$
  FOR I=0 TO -1 STEP 0
      LINPUT# 4, L$, Terminator
      I=(ST<>0)
      IF IS.WINDOWS.FILE THEN GET# 4, XX$
      PRINT# 7, L$
      PRINT ".";
  NEXT
  CLOSE 4:CLOSE 7
  TMP$=TmpFile$+",S,R"
  OPEN 7,DEVICE,7,TMP$
  EFILE$="@:"+THEFILE$+",S,W"
  COLOR VGA.YELLOW
  OPEN 4,DEVICE,4,EFILE$
  FOR I=0 TO -1 STEP 0
      LINPUT# 7, L$
      I=(ST<>0)
      PRINT# 4, L$
      PRINT "."
  NEXT
  PRINT:PRINT
  CLOSE 4:CLOSE 7
  COMMAND$="S:"+TmpFile$:GOTO DOS.CMD

SET.LINE.END:
   FOR I=0 TO -1 STEP 0
       GET# 4, X$
       C=ASC(X$)
       I=(C=13 OR C=10)
   NEXT I
   IF C=10 THEN LINE.ENDING = 10:RETURN
   LINE.ENDING=13
   GET# 4, X$
   IF X$=CHR$(10) THEN IS.WINDOWS.FILE=-1
   Terminator=LINE.ENDING
   CHANNEL=4:P0=0:P1=0:P2=0:P3=0:GOTO POSITION

CHECK.ERROR:
 IF FC$<>"OK" THEN WAS.ERROR
 RETURN

WAS.ERROR:
 PRINT
 PRINT "AN ERROR OCCURED:";FCode;", ";FC$
 PRINT
 END

Make.Temp.File:
  TmpNum = INT(RND(.)*100)+1
  TmpChar = INT(RND(.)*20)+65
  TmpChar$ = CHR$(TmpChar)
  TmpNum$ = STR$(TmpNum)
  TmpNum$ = MID$(TmpNum$,2)
  TmpFile.Prefix$ = TmpChar$+TmpNum$
  TmpFile$ = TmpFile.Prefix$ + "TMP.TMP"
  OPEN 9,DEVICE,9,"@:"+TmpFile$ + ",S,W"
  PRINT# 9, ""
  CLOSE 9
  GOSUB GETFILECODE
  TMP.SUCCESS=(FC$="OK")
RETURN

CHECK.FOR.FILE:
  OB = PEEK(0)
  BANK 0
  WORKBANK = PEEK($BFFE)
  BANK WORKBANK
  SAddr=$BD00:GOSUB PEEK.STRING
  THEFILE$=""
  File.TCheck = (S$="LOADFILE:")
  IF File.TCheck THEN SAddr=$BD10:GOSUB PEEK.STRING:THEFILE$=S$:S$="DONEREAD:":SAddr=$BD00:GOSUB POKE.STRING
  SAddr = $BE00:GOSUB PEEK.STRING
  LOAD.RETURN = (S$="MYLOADER:")
  IF LOAD.RETURN THEN SAddr=$BE10:GOSUB PEEK.STRING:My.Loader$=S$:POKE $BE00,.
  BANK OB
RETURN

#INCLUDE "BOOKS.BI"
