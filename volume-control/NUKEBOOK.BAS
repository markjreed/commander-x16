#CONTROLCODES 1
#INCLUDE "BOOKS.DEF"
CLS
PRINT "INITIALIZING"
GOSUB VERIFY.GOOD.LOAD
GOSUB GET.TARGET.DIR
COMMAND$="CD:"+TargetDir$
GOSUB DOS.CMD
GOSUB HIDETEXT
VPOKE 1,$FA0C,0:VPOKE 1,$FA0D,0
SCREEN 1

GOSUB GetCWD
FirstDir$=CWD$
GOSUB SET.VGATextColors
REM GOSUB SET.ThinCP437
GOSUB LoadDCharsCP437
COLOR VGA.WHITE, VGA.BLACK2
CH%=176:GOSUB FILL.TEXTSCREEN
GOSUB BOTTOM.BOX
GOSUB UPPER.BOX
GOSUB SHOWTEXT


 PRINT
 GOSUB CONFIRM.NUKE
 GOSUB HOURGLASS.ON
 LOCATE 6,7:PRINT "{GREY 2}** {RED}REMOVAL IN PROGRESS {GREY 2}**";

 TOTAL.SCRATCHED = 0
 DIRS.NUKED = 0
 GOSUB DO.TREE
 FOR STAT.LINE=1 TO 3:GOSUB CLEAR.STATMSG:NEXT
 STAT.LINE=1:STAT.MSG$="{GREY 1} "+FirstDir$+"{LIGHT RED} Removed.":GOSUB SHOW.STATMSG
 STAT.LINE=2:STAT.MSG$="{GREY 1}" + STR$(DIRS.NUKED)+ "{LIGHT BLUE} Folders":GOSUB SHOW.STATMSG
 STAT.LINE=3:STAT.MSG$="{GREY 1}" + STR$(TOTAL.SCRATCHED)+"{LIGHT BLUE} Files":GOSUB SHOW.STATMSG

 GOSUB GetCWD
 COLOR VGA.WHITE,VGA.BLACK2
 GOSUB UPPER.BOX
 LOCATE 16,7:PRINT "{GREY 1} Removal Complete !";
 LOCATE 18,7
 PRINT " Current Working Directory is: {LIGHT RED}";CWD$;
 DC$=CHR$(177):X=20:Y=7:UC=0:COLOR VGA.LITEGREEN,VGA.BLACK2:WD$="Done":GOSUB BIGWORD
 COLOR VGA.YELLOW:WD$="!":X=55:GOSUB BIGWORD
 LOCATE 20,7:PRINT "{LIGHT BLUE} Any Key to Finish up{LIGHT RED}...."
 GOSUB HOURGLASS.OFF
 GOSUB GET.AnyKey


END.AND.CLEANUP:
 VPOKE 1,$FA0C,0:VPOKE 1,$FA0D,0
 SCREEN 0
 LOAD MyRootDir$+"/BOOKMENU.PRG"
 END

#INCLUDE "VCTITLE.BI"

DO.TREE:
   StartDir$=CWD$
NEXT.TREE:
   GOSUB COUNT.FOLDERS
   IF FOLDER.COUNT > 0 THEN KEEP.GOING
   GOSUB KILLFILES
   IF CWD$=StartDir$ THEN FINISH.UP
   COMMAND$="CD:..":GOSUB DOS.CMD
   COMMAND$="RD:"+CLIPPER$
   STAT.LINE=1:STAT.MSG$=" {GREY 1}EXECUTING: {LIGHT RED}"+LEFT$(COMMAND$,55):GOSUB SHOW.STATMSG
   GOSUB DOS.CMD:DIRS.NUKED = DIRS.NUKED+1
   STAT.LINE=3:STAT.MSG$="{LIGHT BLUE}"+STR$(DIRS.NUKED)+" {GREY 1}Folders Removed.":GOSUB SHOW.STATMSG
   GOTO NEXT.TREE
KEEP.GOING:
   GOSUB GET.FIRST.FOLDER
   COMMAND$="CD:"+FIRST.FOLDER$
   STAT.LINE=1:STAT.MSG$=" {GREY 1}EXECUTING: {LIGHT RED}"+LEFT$(COMMAND$,55):GOSUB SHOW.STATMSG
   GOSUB DOS.CMD
   GOTO NEXT.TREE
FINISH.UP:
   PRINT:PRINT
   IF CWD$="/" THEN RETURN
   COMMAND$="CD:..":GOSUB DOS.CMD
   STAT.LINE=3:STAT.MSG$=" {GREY 1}REMOVING: {GREY 2}"+LEFT$(StartDir$,55):GOSUB SHOW.STATMSG
   COMMAND$="RD:"+CWD$:GOSUB DOS.CMD:DIRS.NUKED = DIRS.NUKED+1
   RETURN


COUNT.FOLDERS:
     CLOSE 1
     OPEN 1, DEVICE, 0, "$=D"
     BINPUT#1, LA$, 2: REM SKIP LOAD ADDRESS
     IF ST AND 64 THEN D$ = "UNEXPECTED EOF 3": DD = -1: RETURN
     FOLDER.COUNT = 0
     FOR I = 0 TO -1 STEP 0
        BINPUT#1, LL$, 2:REM SKIP NEXT-LINE POINTER
        IF ST AND 64 THEN DONE.FOLDER
        GET#1, L$:L = ASC(L$): REM LOW BYTE OF SIZE
        IF ST AND 64 THEN DS$ = "UNEXPECTED EOF 4": DD = -1: RETURN
        GET#1, H$:H = ASC(H$):REM HIGH BYTE OF SIZE
        IF ST AND 64 THEN DS$ = "UNEXPECTED EOF 5": DD = -1: RETURN
        ## USE IF I NEED FILE SIZE DD%(DD) = FN SW(256*H + L)
        LINPUT#1, X$, 34:REM SKIP TO FIRST QUOTE
        IF ST AND 64 THEN DONE.FOLDER
        LINPUT#1, X$, 34
        IF X$<>"." AND X$<>".." THEN FOLDER.COUNT=FOLDER.COUNT+1
        LINPUT#1, X$, 0
      NEXT
DONE.FOLDER:
      FOLDER.COUNT = FOLDER.COUNT - 1
      CLOSE 1
      RETURN

GET.FIRST.FOLDER:
     FIRST.FOLDER$=""
     CLOSE 1
     OPEN 1, DEVICE, 0, "$=D"
     BINPUT#1, LA$, 2: REM SKIP LOAD ADDRESS
     IF ST AND 64 THEN D$ = "UNEXPECTED EOF 3": DD = -1: RETURN
     DD = 0
     FOR I = 0 TO -1 STEP 0
        BINPUT#1, LL$, 2:REM SKIP NEXT-LINE POINTER
        IF ST AND 64 THEN DONE.FOLDER2
        GET#1, L$:L = ASC(L$): REM LOW BYTE OF SIZE
        IF ST AND 64 THEN DS$ = "UNEXPECTED EOF 4": DD = -1: RETURN
        GET#1, H$:H = ASC(H$):REM HIGH BYTE OF SIZE
        IF ST AND 64 THEN DS$ = "UNEXPECTED EOF 5": DD = -1: RETURN
        ## USE IF I NEED FILE SIZE DD%(DD) = FN SW(256*H + L)
        LINPUT#1, X$, 34:REM SKIP TO FIRST QUOTE
        IF ST AND 64 THEN DONE.FOLDER2
        LINPUT#1, X$, 34
        IF X$<>"." AND X$<>".." AND DD<> 0 THEN FIRST.FOLDER$=X$:GOTO DONE.FOLDER2
        LINPUT#1, X$, 0
        DD=DD+1
      NEXT
DONE.FOLDER2:
      CLOSE 1
      RETURN

KILLFILES:
 IF Was.Tested THEN START.THE.KILL
 GOSUB TestScratch
START.THE.KILL:
 GOSUB GetCWD
 L=LEN(CWD$)
 L1=L
 FOR I=0 TO -1 STEP 0
     L=L-1
     I=(MID$(CWD$,L,1)="/")
 NEXT I
 CLIPPER$=RIGHT$(CWD$,L1-L)
 GOSUB GETFILECODE
 DIRFILES = 0
 STAT.LINE=1:STAT.MSG$= " {GREY 1}DELETING ALL FILES IN {LIGHT RED}"+LEFT$(CWD$,50):GOSUB SHOW.STATMSG
 COMMAND$="S:*"
 DOTS.DONE=0:RFILES.DONE=DOTS.DONE
 IF HostFS.Flag THEN HOST.FILE.SYSTEM.LOOP

  FOR I=0 TO -1 STEP 0
         IF RFILES.DONE THEN SKIP.RFILES
            GOSUB DOS.CMD
            IF FCODE=1 THEN DIRFILES=DIRFILES+FCode2
            IF FCode2=0 THEN RFILES.DONE=-1
SKIP.RFILES:
        IF DOTS.DONE THEN DO.LOOP
           COMMAND$="S:.*"
           GOSUB DOS.CMD
           IF FCode=1 THEN DIRFILES=DIRFILES+FCode2
           IF FCode2=0 THEN DOTS.DONE=-1
           COMMAND$="S:*"
DO.LOOP:
     I = (DOTS.DONE AND RFILES.DONE)
 NEXT
 GOTO DONE.SCRATCHING

HOST.FILE.SYSTEM.LOOP:
 FOR I=0 TO -1 STEP 0
        IF RFILES.DONE THEN SKIP.RFILESH
           GOSUB DOS.CMD
           IF FCODE=1 THEN DIRFILES=DIRFILES+1
           IF FCode>1 THEN RFILES.DONE=-1
SKIP.RFILESH:
       IF DOTS.DONE THEN DO.LOOPH
          COMMAND$="S:.*"
          GOSUB DOS.CMD
          IF FCODE=1 THEN DIRFILES=DIRFILES+1
          IF FCODE>1 THEN DOTS.DONE=-1
          COMMAND$="S:*"
DO.LOOPH:
     I = (DOTS.DONE AND RFILES.DONE)
 NEXT

DONE.SCRATCHING:

 STAT.LINE=2:STAT.MSG$="{LIGHT BLUE}"+STR$(DIRFILES) + " {GREY 1}SCRATCHED":GOSUB SHOW.STATMSG
 TOTAL.SCRATCHED = TOTAL.SCRATCHED+DIRFILES
 STAT.LINE=3:STAT.MSG$=" {GREY 1}TOTAL SCRATCHED: {LIGHT BLUE}"+STR$(TOTAL.SCRATCHED):GOSUB SHOW.STATMSG
RETURN

TestScratch:
  TestNum = INT(RND(.)*100)+1
  TestChar = INT(RND(.)*20)+65
  TestChar$ = CHR$(TestChar)
  TestNum$ = STR$(TestNum)
  TestNum$ = MID$(TestNum$,2)
  TestFile.Prefix$ = TestChar$+TestNum$
  TestFile1$ = TestFile.Prefix$ + "ABC.TST"
  TestFile2$ = TestFile.Prefix$ + "XYZ.TST"
  OPEN 9,DEVICE,9,"@:"+TestFile1$ + ",S,W"
  PRINT# 9, "TEST IT"
  CLOSE 9
  OPEN 9,DEVICE,9,"@:"+TestFile2$ + ",S,W"
  PRINT# 9, "TEST IT"
  CLOSE 9
  COMMAND$="S:"+TestFile.Prefix$ + "*"
  GOSUB DOS.CMD
  IF FCode=1 AND FCode2 >= 2 THEN HostFS.Flag = 0:GOTO Done.ScratchTest
  HostFS.Flag = -1
Done.ScratchTest:
 Was.Tested = -1
RETURN



CONFIRM.NUKE:
  IF CWD$="/" THEN HELL.NO
  LOCATE 6,7:PRINT "{GREY 2}CURRENT WORKING DIRECTORY"
  LOCATE 7,7:PRINT "{LIGHT BLUE}-------------------------"
  LOCATE 9,9:PRINT "{LIGHT RED}  ";CWD$

  LOCATE 13,7:PRINT "{GREY 1} This program will {RED}COMPLETELY REMOVE {LIGHT RED}ALL";
  LOCATE 14,7:PRINT "{GREY 1} files in the Current Book folder.";
  LOCATE 16,7:PRINT "{GREY 1} It will then remove{LIGHT RED} ";CWD$;" {GREY 1}Itself {LIGHT BLUE}!!!!"
  LOCATE 19,7:PRINT "{LIGHT RED} ARE YOU SURE YOU WANT TO DO THIS ? ":PRINT
  LOCATE 20,7:PRINT "{GREY 1} ENTER [{LIGHT RED}YES{GREY 1}] TO PROCEED:{LIGHT BLUE}";
  COLOR VGA.YELLOW,VGA.DARKGRAY
  IT=4:ML=3:IX=32:IY=20:GOSUB STRINGGET
  Y$=IS$:COLOR VGA.WHITE,VGA.BLACK2
  IF Y$="YES" THEN NEXT.YES
  GOTO ABORT.NUKE
NEXT.YES:
 LOCATE 19,7:PRINT "{RED} ARE YOU {LIGHT BLUE}REALLY SURE{RED} YOU WANT TO DO THIS {LIGHT RED}?? "
 LOCATE 20,7:PRINT "{GREY 1} ENTER [{LIGHT RED}YES{GREY 1}] TO PROCEED:{LIGHT BLUE}";
 COLOR VGA.YELLOW,VGA.DARKGRAY
 IT=4:ML=3:IX=32:IY=20:GOSUB STRINGGET
 Y$=IS$:COLOR VGA.WHITE,VGA.BLACK2
 IF Y$="YES" THEN RETURN
ABORT.NUKE:
 COMMAND$="CD:..":GOSUB DOS.CMD
 GOTO END.AND.CLEANUP

HELL.NO:
 FOR I=1 TO 5:FMDRUM 7,87:SLEEP 10:NEXT
 FOR S=13 TO 27 STEP 2:FMDRUM 7,87:SLEEP S:NEXT
 STAT.LINE = 1:STAT.MSG$="{LIGHT RED}     Danger{LIGHT BLUE}!":GOSUB SHOW.STATMSG
 STAT.LINE = 2:STAT.MSG$=      "{RED}          Will Robinson":GOSUB SHOW.STATMSG
 STAT.LINE = 3:STAT.MSG$="     {LIGHT RED}Danger{LIGHT BLUE}! {LIGHT RED}Danger{LIGHT BLUE}! {LIGHT RED}Danger{LIGHT BLUE}! ":GOSUB SHOW.STATMSG
 COLOR VGA.WHITE,VGA.BLACK2
 LOCATE 8,7:PRINT "{GREY 1} THE CURRENT WORKING DIRECTORY IS YOUR {PURPLE} ** ROOT ** {GREY 1} DIRECTORY !!";
 LOCATE 12,7:PRINT " IF YOU REALLY WANT TO DO THIS THEN YOU MUST EDIT ";
 LOCATE 13,7:PRINT " SOME {LIGHT RED}SOURCE CODE {LIGHT BLUE}!!";
 LOCATE 17,7:PRINT " {GREY 1}I REFUSE TO LET {GREY 2}MY CODE{GREY 1} ";
 PRINT "PROCEED WITH THIS DISASTER !!!!!";

 GOSUB GET.AnyKey
 GOTO END.AND.CLEANUP


UPPER.BOX:
 COLOR VGA.RED,VGA.BLACK2
 X=5:Y=4:W=72:H=18:GOSUB TEXTBOX
 RETURN

BOTTOM.BOX:
 COLOR VGA.ORANGE, BLACK
 LOCATE 24,2:PRINT CHR$(219);
 FOR Y = 25 TO 27:LOCATE Y,79:PRINT CHR$(222);:LOCATE Y,2:PRINT CHR$(221);:NEXT
 LOCATE 28,2:PRINT CHR$(219);
 LOCATE 24,79:PRINT CHR$(219);
 LOCATE 28,79:PRINT CHR$(219);
 LOCATE 24,3:PRINT RPT$(223,76);
 LOCATE 28,3:PRINT RPT$(220,76);
 FOR I=25 TO 27:LOCATE I,3:PRINT RPT$(32,76);:NEXT
 RETURN

SHOW.STATMSG:
 COLOR VGA.WHITE, BLACK
 LOCATE 24+STAT.LINE,4
 PRINT RPT$(32,74);
 LOCATE 24+STAT.LINE,4
 PRINT STAT.MSG$;
 GOTO RestoreCursor.AndColor

CLEAR.STATMSG:
 GOSUB SaveCursor.AndColor
 COLOR VGA.WHITE,BLACK
 LOCATE 24+STAT.LINE,4
 PRINT RPT$(32,74);
 GOTO RestoreCursor.AndColor

BIGWORD:
  L=LEN(WD$):SX=X
  OB=PEEK(1):BANK PEEK(0),6
  FOR K=1 TO L
    CC=ASC(MID$(WD$,K,1))
    IF (CC>=64 AND CC<=90) THEN AA=64:GOTO SKIP0
    IF (CC>=97 AND CC<=122) THEN AA=-32:GOTO SKIP0
    AA=0
SKIP0:
    CA=$C000+8*(CC-AA)
    FOR I=1 TO 8
      CM(I)=PEEK(CA+(I-1)):CM$(I)=""
    NEXT I
    IF DC$="" THEN DC$=CHR$(CC)
    TD=8:IF CC=32 THEN TD=4
    FOR J=1 TO TD
        RESTORE BITMAPS
        FOR CT=1 TO 8
            READ CP
            IF (CP AND CM(J)) THEN CM$(J)=CM$(J)+DC$:GOTO SKIPSPC
            CM$(J)=CM$(J)+CHR$(32)
      SKIPSPC:
        NEXT CT
        LOCATE Y+(J-1),X:IF UC=1 THEN COLOR TC%(1,J),TC%(2,J)
        PRINT CM$(J);
    NEXT J
    IF DC$=CHR$(CC) THEN DC$=""
    X=X+TD:NEXT K
  X=SX:BANK PEEK(0),OB:RETURN

BITMAPS:
  DATA 128,64,32,16,8,4,2,1,0


#INCLUDE "BOOKS.BI"
