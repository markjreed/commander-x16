#CONTROLCODES 1
#INCLUDE "/BASIC/INCLUDES/STANDARD.DEF"
#INCLUDE "/BASIC/INCLUDES/DIRSUBS.DEF"

SCREEN 9:PRINT "{ISO ON}":COLOR 1,0:CLS
PRINT " INITIALIZING..."
GOSUB GetCWD
BORDER.COLOR = $F0

 PRINT
 GOSUB CONFIRM.NUKE
 TOTAL.SCRATCHED = 0
 DIRS.NUKED = 0
 GOSUB DO.TREE
 POKE $9F2C, 0
 PRINT:PRINT:PRINT
 PRINT "{YEL} ";DIRS.NUKED;" {WHT}FOLDERS REMOVED ":PRINT:PRINT
 PRINT "{YEL} ";TOTAL.SCRATCHED;" {WHT}FILES REMOVED ":PRINT:PRINT

 GOSUB GetCWD
 PRINT "{WHT} Current Working Directory is: {LIGHT GREEN}";CWD$
 PRINT
 END


DO.TREE:
   StartDir$=CWD$
NEXT.TREE:
   GOSUB COUNT.FOLDERS
   IF FOLDER.COUNT > 0 THEN KEEP.GOING
   GOSUB KILLFILES
   IF CWD$=StartDir$ THEN FINISH.UP
   COMMAND$="CD:..":GOSUB DOS.CMD
   COMMAND$="RD:"+CLIPPER$
   PRINT:GOSUB PULSATE.BORDER
   PRINT " {WHT}EXECUTING: {LIGHT GREEN}";COMMAND$
   PRINT
   GOSUB DOS.CMD:DIRS.NUKED = DIRS.NUKED+1
   PRINT "{GRN}{CR}",FCODE,FC$;"{CR}{CR}{WHT}"
   GOTO NEXT.TREE
KEEP.GOING:
   GOSUB GET.FIRST.FOLDER
   COMMAND$="CD:"+FIRST.FOLDER$
   GOSUB PULSATE.BORDER
   PRINT "{WHT} EXECUTING: {LIGHT GREEN}";COMMAND$
   GOSUB DOS.CMD
   GOTO NEXT.TREE
FINISH.UP:
   PRINT:PRINT
   IF CWD$="/" THEN RETURN
   COMMAND$="CD:..":GOSUB DOS.CMD
   PRINT " {WHT}REMOVING: {LIGHT GREEN}";StartDir$
   COMMAND$="RD:"+CWD$:GOSUB DOS.CMD:DIRS.NUKED = DIRS.NUKED+1
   RETURN


COUNT.FOLDERS:
     CLOSE 1
     OPEN 1, DEVICE, 0, "$=D"
     BINPUT#1, LA$, 2: REM SKIP LOAD ADDRESS
     IF ST AND 64 THEN D$ = "UNEXPECTED EOF 3": DD = -1: RETURN
     FOLDER.COUNT = 0
     FOR I = 0 TO -1 STEP 0
        BINPUT#1, LL$, 2:REM SKIP NEXT-LINE POINTER
        IF ST AND 64 THEN DONE.FOLDER
        GET#1, L$:L = ASC(L$): REM LOW BYTE OF SIZE
        IF ST AND 64 THEN DS$ = "UNEXPECTED EOF 4": DD = -1: RETURN
        GET#1, H$:H = ASC(H$):REM HIGH BYTE OF SIZE
        IF ST AND 64 THEN DS$ = "UNEXPECTED EOF 5": DD = -1: RETURN
        ## USE IF I NEED FILE SIZE DD%(DD) = FN SW(256*H + L)
        LINPUT#1, X$, 34:REM SKIP TO FIRST QUOTE
        IF ST AND 64 THEN DONE.FOLDER
        LINPUT#1, X$, 34
        IF X$<>"." AND X$<>".." THEN FOLDER.COUNT=FOLDER.COUNT+1
        LINPUT#1, X$, 0
      NEXT
DONE.FOLDER:
      FOLDER.COUNT = FOLDER.COUNT - 1
      CLOSE 1
      RETURN

GET.FIRST.FOLDER:
     FIRST.FOLDER$=""
     CLOSE 1
     OPEN 1, DEVICE, 0, "$=D"
     BINPUT#1, LA$, 2: REM SKIP LOAD ADDRESS
     IF ST AND 64 THEN D$ = "UNEXPECTED EOF 3": DD = -1: RETURN
     DD = 0
     FOR I = 0 TO -1 STEP 0
        BINPUT#1, LL$, 2:REM SKIP NEXT-LINE POINTER
        IF ST AND 64 THEN DONE.FOLDER2
        GET#1, L$:L = ASC(L$): REM LOW BYTE OF SIZE
        IF ST AND 64 THEN DS$ = "UNEXPECTED EOF 4": DD = -1: RETURN
        GET#1, H$:H = ASC(H$):REM HIGH BYTE OF SIZE
        IF ST AND 64 THEN DS$ = "UNEXPECTED EOF 5": DD = -1: RETURN
        ## USE IF I NEED FILE SIZE DD%(DD) = FN SW(256*H + L)
        LINPUT#1, X$, 34:REM SKIP TO FIRST QUOTE
        IF ST AND 64 THEN DONE.FOLDER2
        LINPUT#1, X$, 34
        IF X$<>"." AND X$<>".." AND DD<> 0 THEN FIRST.FOLDER$=X$:GOTO DONE.FOLDER2
        LINPUT#1, X$, 0
        DD=DD+1
      NEXT
DONE.FOLDER2:
      CLOSE 1
      RETURN


KILLFILES:
 GOSUB GetCWD
 L=LEN(CWD$)
 L1=L
 FOR I=0 TO -1 STEP 0
     L=L-1
     I=(MID$(CWD$,L,1)="/")
 NEXT I
 CLIPPER$=RIGHT$(CWD$,L1-L)
 GOSUB GETFILECODE
 DIRFILES = 0
 PRINT " {WHT}DELETING ALL FILES IN {LIGHT GREEN}";CWD$;" {YEL}";
 COMMAND$="S:*"
 DOTS.DONE=0:RFILES.DONE=DOTS.DONE
 FOR I=0 TO -1 STEP 0
     GOSUB PULSATE.BORDER
     IF RFILES.DONE THEN SKIP.RFILES
        GOSUB DOS.CMD
        IF FCODE=1 THEN DIRFILES=DIRFILES+FCODE:PRINT ".";
        IF FCODE<>1 THEN RFILES.DONE=-1
SKIP.RFILES:
     IF DOTS.DONE THEN DO.LOOP
        COMMAND$="S:.*"
        GOSUB DOS.CMD
        IF FCODE=1 THEN DIRFILES=DIRFILES+FCODE:PRINT ".";
        IF FCODE<>1 THEN DOTS.DONE=-1
        COMMAND$="S:*"
DO.LOOP:
     I = (DOTS.DONE AND RFILES.DONE)
 NEXT

 PRINT:PRINT "{WHT}"
 PRINT DIRFILES;" {LIGHT GREEN}SCRATCHED"
 TOTAL.SCRATCHED = TOTAL.SCRATCHED+DIRFILES
 GOSUB GETFILECODE
 RETURN

PULSATE.BORDER:
 POKE $9F2C, BORDER.COLOR
 BORDER.COLOR = (BORDER.COLOR + 1) AND 255
 RETURN


CONFIRM.NUKE:
  CLS
  IF CWD$="/" THEN HELL.NO
  PRINT
  PRINT "  {LIGHT RED}CURRENT WORKING DIRECTORY"
  PRINT "  {YEL}-------------------------"
  PRINT:PRINT "{LIGHT GREEN}  ";CWD$
  PRINT "{CR}{CR}{CR}{WHT}"
  PRINT " This program will {RED}COMPLETELY REMOVE {LIGHT GREEN}ALL"
  PRINT "{WHT} files and Folders in this Directory.":PRINT
  PRINT " It will then remove{LIGHT RED} ";CWD$;" {WHT}Itself {LIGHT GREEN}!!!!"
  PRINT "{CR}"
  PRINT "{LIGHT GREEN} ARE YOU SURE YOU WANT TO DO THIS ? ":PRINT
  INPUT "{WHT}TYPE [{LIGHT GREEN}YES{WHT}] TO PROCEED:{YEL}"; Y$
  IF Y$="YES" THEN NEXT.YES
  PRINT:END
NEXT.YES:
 PRINT "{CR}
 PRINT "{RED} ARE YOU {LIGHT GREEN}REALLY SURE{RED} YOU WANT TO DO THIS {LIGHT GREEN}?? ":PRINT
 INPUT "{WHT}TYPE [{LIGHT GREEN}YES{WHT}] TO PROCEED:{YEL}"; Y$
 IF Y$="YES" THEN NEXT.YES2
 PRINT:END
NEXT.YES2:
 PRINT "{CR}"
 PRINT "{LIGHT RED} ARE YOU {GRN}REALLY{LIGHT GREEN}, REALLY SURE{LIGHT RED} YOU WANT TO DO THIS {GRN}?? ":PRINT
 INPUT "{WHT}TYPE [{LIGHT GREEN}YES{WHT}] TO PROCEED:{YEL}"; Y$
 IF Y$="YES" THEN RETURN
 PRINT:END

HELL.NO:
 PRINT
 PRINT "{LIGHT GREEN} THE CURRENT WORKING DIRECTORY IS YOUR {RED} ** ROOT ** {LIGHT GREEN} DIRECTORY !!"
 PRINT:PRINT
 PRINT " IF YOU REALLY WANT TO DO THIS THEN YOU MUST EDIT {CR} SOME SOURCE CODE !!"
 PRINT
 PRINT " {GRN}I REFUSE TO LET {LIGHT GREEN}MY CODE{GRN} PROCEED WITH THIS DISASTER !!!!!"
 PRINT
 FOR I=1 TO 3:PRINT CHR$(7);:NEXT
 END




#INCLUDE "/BASIC/INCLUDES/STANDARD.BI"
#INCLUDE "/BASIC/INCLUDES/DIRSUBS.BI"
